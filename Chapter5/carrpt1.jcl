//HERCAR1 JOB (COBOL),
//             'Car Lst',
//             CLASS=A,
//             MSGCLASS=A,
//             REGION=8M,TIME=1440,
//             MSGLEVEL=(1,1),
//  USER=HERC01,PASSWORD=CUL8TR
//CARRPT1   EXEC COBUCG,
//         PARM.COB='FLAGW,LOAD,SUPMAP,SIZE=2048K,BUF=1024K'
//COB.SYSPUNCH DD DUMMY
//COB.SYSIN    DD *
000001 IDENTIFICATION DIVISION.
000002 PROGRAM-ID. RWEX01.
000003 AUTHOR. BRAD RIGG VIA JAY MOSELEY.
000004 DATE-WRITTEN. AUG, 2020.
000005 DATE-COMPILED.
000006      
000007* ************************************************************* *
000008* REPORT WRITER EXAMPLE #1.                                     *
000009* ************************************************************* *
000010      
000011 ENVIRONMENT DIVISION.
000012 CONFIGURATION SECTION.
000013 SOURCE-COMPUTER. IBM-370.
000014 OBJECT-COMPUTER. IBM-370.
000015      
000016 INPUT-OUTPUT SECTION.
000017 FILE-CONTROL.
000018      
000019     SELECT TRANSACTION-DATA
000020         ASSIGN TO UT-S-CARS.
000021      
000022     SELECT REPORT-FILE
000023         ASSIGN TO UR-S-SYSPRINT.
000024      
000025 DATA DIVISION.
000026 FILE SECTION.
000027      
000028 FD  TRANSACTION-DATA
000029     LABEL RECORDS ARE OMITTED
000030     BLOCK CONTAINS 0 RECORDS
000031     RECORD CONTAINS 80 CHARACTERS
000032     DATA RECORD IS TRANSACTION-RECORD.
000033      
000034 01  TRANSACTION-RECORD.
000035     03  TR-CAR-MAKE             PIC X(16).
000036     03  TR-CAR-MODEL            PIC X(20).
000037     03  TR-FY-PROD              PIC 9(04).
000038     03  FILLER                  PIC X(40).
000039      
000040 FD  REPORT-FILE
000041     LABEL RECORDS ARE OMITTED
000042     REPORT IS CAR-REPORT.
000043      
000044 WORKING-STORAGE SECTION.
000045 77  END-OF-FILE-SWITCH          PIC X(1)    VALUE 'N'.
000046     88  END-OF-FILE                         VALUE 'Y'.
000047      
000048 REPORT SECTION.
000049 RD  CAR-REPORT
000050     PAGE LIMIT IS 66 LINES
000051     HEADING 1
000052     FIRST DETAIL 5
000053     LAST DETAIL 58.
000054      
000055 01  PAGE-HEAD-GROUP TYPE PAGE HEADING.
000056     02  LINE 1.
000057         03  COLUMN 27   PIC X(30) VALUE
000058             'CAR MAKE AND MODEL R E P O R T'.
000059     02  LINE PLUS 2.
000060         03  COLUMN 01   PIC X(09) VALUE 'CAR MAKE.'.
000061         03  COLUMN 18   PIC X(10) VALUE 'CAR MODEL.'.
000062         03  COLUMN 41   PIC X(09) VALUE 'F/Y PROD.'.
000063      
000064 01  CAR-DETAIL TYPE DETAIL.
000065     02  LINE PLUS 1.
000066         03  COLUMN 03   PIC X(16) SOURCE TR-CAR-MAKE.
000067         03  COLUMN 19   PIC X(20) SOURCE TR-CAR-MODEL.
000068         03  COLUMN 42   PIC 9(04)  SOURCE TR-FY-PROD.
000069      
000070 PROCEDURE DIVISION.
000071      
000072 000-INITIATE.
000073      
000074     OPEN INPUT TRANSACTION-DATA,
000075          OUTPUT REPORT-FILE.
000076      
000077     INITIATE CAR-REPORT.
000078      
000079     READ TRANSACTION-DATA
000080         AT END
000081             MOVE 'Y' TO END-OF-FILE-SWITCH.
000082*    END-READ.
000083      
000084     PERFORM 100-PROCESS-TRANSACTION-DATA THRU 199-EXIT
000085         UNTIL END-OF-FILE.
000086      
000087 000-TERMINATE.
000088     TERMINATE CAR-REPORT.
000089      
000090     CLOSE TRANSACTION-DATA,
000091           REPORT-FILE.
000092     DISPLAY 'REPORT FINISHED' UPON CONSOLE.      
000093     STOP RUN.
000094      
000095 100-PROCESS-TRANSACTION-DATA.
000096     GENERATE CAR-DETAIL.
000097     READ TRANSACTION-DATA
000098         AT END
000099             MOVE 'Y' TO END-OF-FILE-SWITCH.
000100*    END-READ.
000101      
000102 199-EXIT.
000103     EXIT.
000104      
 /*
//COB.SYSLIB   DD DSNAME=SYS1.COBLIB,DISP=SHR
//GO.SYSIN  DD * 
//GO.SYSPRINT DD SYSOUT=*
//CARS DD DSN=HERC01.TEST.CNTL(CARS),DISP=SHR
/*
//        
        